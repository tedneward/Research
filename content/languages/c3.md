title=c3c
tags=language, native, procedural
summary=A C-like language trying to be "an incremental improvement over C" rather than a whole new language.
~~~~~~

[Website](http://www.c3-lang.org/) | [Source](https://github.com/c3lang/c3c) | [Handmade Project (website)](https://c3.handmade.network/)

C3 is a programming language that builds on the syntax and semantics of the C language, with the goal of evolving it while still retaining familiarity for C programmers.

It's an evolution, not a revolution: the C-like for programmers who like C.

Design Principles

* Procedural "get things done"-type of language.
* Try to stay close to C - only change what's really necessary.
* C ABI compatibility and excellent C integration.
* Learning C3 should be easy for a C programmer.
* Data is inert.
* Avoid "big ideas" & the "more is better" fallacy.
* Introduce some higher level conveniences where the value is great.

C3 owes its inspiration to the [C2](../c2) language: to iterate on top of C without trying to be a whole new language.

In what ways does C3 differ from C?

* No mandatory header files
* New semantic macro system
* Module based name spacing
* Subarrays (slices)
* Compile time reflection
* Enhanced compile time execution
* Generics based on generic modules
* "Result"-based zero overhead error handling
* Defer
* Value methods
* Associated enum data
* No preprocessor
* Less undefined behaviour and added runtime checks in "safe" mode
* Limited operator overloading to enable userland dynamic arrays
* Optional pre and post conditions

### Examples

Stack module
```
module stack {Type};
// Above: the parameterized type is applied to the entire module.

struct Stack
{
    usz capacity;
    usz size;
    Type* elems;
}

// The type methods offers dot syntax calls,
// so this function can either be called 
// Stack.push(&my_stack, ...) or
// my_stack.push(...)
fn void Stack.push(Stack* this, Type element)
{
    if (this.capacity == this.size)
    {
        this.capacity *= 2;
        if (this.capacity < 16) this.capacity = 16;
        this.elems = realloc(this.elems, Type.sizeof * this.capacity);
    }
    this.elems[this.size++] = element;
}

fn Type Stack.pop(Stack* this)
{
    assert(this.size > 0);
    return this.elems[--this.size];
}

fn bool Stack.empty(Stack* this)
{
    return !this.size;
}
```

Using the stack:
```
import stack;

// Define our new types, the first will implicitly create 
// a complete copy of the entire Stack module with "Type" set to "int"
alias IntStack = Stack {int};
// The second creates another copy with "Type" set to "double"
alias DoubleStack = Stack {double};

// If we had added "alias IntStack2 = Stack {int}"
// no additional copy would have been made (since we already
// have an parameterization of Stack {int} so it would
// be same as declaring IntStack2 an alias of IntStack

// Importing an external C function is straightforward
// here is an example of importing libc's printf:
extern fn int printf(char* format, ...);

fn void main()
{
    IntStack stack;
    // Note that C3 uses zero initialization by default
    // so the above is equivalent to IntStack stack = {};
    
    stack.push(1);
    // The above can also be written IntStack.push(&stack, 1); 
    
    stack.push(2);
    
    // Prints pop: 2
    printf("pop: %d\n", stack.pop());
    // Prints pop: 1
    printf("pop: %d\n", stack.pop());
    
    DoubleStack dstack;
    dstack.push(2.3);
    dstack.push(3.141);
    dstack.push(1.1235);
    // Prints pop: 1.123500
    printf("pop: %f\n", dstack.pop());
}
```

